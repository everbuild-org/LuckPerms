import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java-library"
}

// minestom requires java 21+
sourceCompatibility = 21
targetCompatibility = 21

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io/' }
}

dependencies {
    api(project(":common")) {
        // exclude default config providers as they are inaccessible from the minestom module anyway
        exclude group: "org.spongepowered", module: "configurate-yaml"
        exclude group: "org.spongepowered", module: "configurate-gson"
        exclude group: "org.spongepowered", module: "configurate-hocon"
        exclude group: "me.lucko.configurate", module: "configurate-toml"
    }
    compileOnly "net.minestom:minestom-snapshots:1c528d8ae2"

    // testing
    testImplementation project(":common")
    testImplementation "net.minestom:minestom-snapshots:1_20_5-b04ef5189f"
    testImplementation "commons-net:commons-net:3.10.0" // fix vulnerability in minestom
    testImplementation "ch.qos.logback:logback-classic:1.4.14" // logger
    testImplementation "org.spongepowered:configurate-hocon:3.7.2" // configuration using hocon
}

task processSource(type: Sync) {
    from sourceSets.main.java
    filter(ReplaceTokens, tokens: [VERSION: project.ext.fullVersion])
    into project.getLayout().getBuildDirectory().get().dir("src")
}

compileJava {
    source = processSource.outputs
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = 'hyperaSnapshots'
            url = 'https://repo.hypera.dev/snapshots'
            credentials(PasswordCredentials)
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = 'minestom'

            from components.java

            pom {
                name = 'LuckPerms Minestom'
                description = 'A permissions plugin for Minestom servers.'
                url = 'https://github.com/LooFifteen/LuckPerms'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'lucko'
                        name = 'Luck'
                        url = 'https://lucko.me'
                        email = 'git@lucko.me'
                    }
                    developer {
                        id = 'LooFifteen'
                        name = 'LU15'
                        url = 'https://lu15.dev'
                        email = 'luis@lu15.dev'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/LooFifteen/LuckPerms.git'
                    developerConnection = 'scm:git:git@github.com:LooFifteen/LuckPerms.git'
                    url = 'https://github.com/LooFifteen/LuckPerms'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/LooFifteen/LuckPerms/issues'
                }
            }
        }
    }
}